import static org.apache.tools.ant.taskdefs.condition.Os.*

static def isLinuxOrMacOs() {
    return isFamily(FAMILY_MAC) || isFamily(FAMILY_UNIX)
}

task copyGitHooks(type: Copy) {
    description 'Copies the git hooks from config/git-hooks to the .git folder.'
    from("${rootDir}/config/git-hooks/") {
        include '**/*.sh'
        rename '(.*).sh', '$1'
    }
    into "${rootDir}/.git/hooks"
    onlyIf { isLinuxOrMacOs() }
}

task installGitHooks(type: Exec) {
    description 'Installs the pre-commit git hooks from config/git-hooks.'
    group 'git hooks'
    workingDir rootDir
    commandLine 'chmod'
    args '-R', '+x', '.git/hooks/'
    dependsOn copyGitHooks
    onlyIf { isLinuxOrMacOs() }
    doLast { logger.info('Git hook installed successfully.') }
}

afterEvaluate {
    // We install the hook at the first occasion
    tasks['clean'].dependsOn installGitHooks
    tasks['assemble'].dependsOn installGitHooks
}
